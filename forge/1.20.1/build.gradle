plugins {
    id 'multiloader-loader'
    id 'net.minecraftforge.gradle' version '[6.0.24,6.2)'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT' apply true
}

mixin {
    add(sourceSets.main, "${mod_id}.refmap.json")

    config("${mod_id}.mixins.json")
    config("${mod_id}.forge.mixins.json")
}

minecraft {
    mappings channel: "official", version: minecraft_version
    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory file("${rootProject.projectDir}/.runs/${platform}/${project.name}/${it.name}")
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        client {
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        server {
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        data {
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    compileOnly project(":common")
    compileOnly 'org.jetbrains:annotations:26.0.2'

    compileOnly "mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"
    // runtimeOnly "mezz.jei:jei-${minecraft_version}-forge:${jei_version}" // is not working...

    annotationProcessor("org.spongepowered:mixin:0.8.5-SNAPSHOT:processor")
}

jar {
    finalizedBy('reobfJar')
    manifest.attributes([
            "MixinConfigs": "${mod_id}.mixins.json,${mod_id}.forge.mixins.json"
    ])
}