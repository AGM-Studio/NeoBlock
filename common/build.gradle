plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev'
}


// https://parchmentmc.org/docs/getting-started#choose-a-version
// https://projects.neoforged.net/neoforged/neoform
moduleExpandProps = [
    'minecraft_version'         : minecraft_version,
    'minecraft_version_range'   : minecraft_version_range,
    'parchment_minecraft'       : parchment_minecraft,
    'parchment_version'         : parchment_version,
    'neo_form_version'          : neo_form_version,
]

neoForge {
    neoFormVersion = neo_form_version
    // Automatically enable AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    // fabric and neoforge both bundle mixinextras, so it is safe to use it in common
    compileOnly group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
    annotationProcessor group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}

